plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.3'
	id 'io.spring.dependency-management' version '1.1.3'
	id "io.freefair.lombok" version "8.3"
}

group = 'de.thm'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

tasks.named('test') {
	useJUnitPlatform()
}

processResources {
	dependsOn 'copyFrontend'
}

tasks.register('buildFrontend', Exec) {
	def npm = System.getProperty("os.name").toLowerCase().contains("win") ? 'npm.cmd' : 'npm'
	workingDir '../client'
	commandLine npm, 'install'
	commandLine npm, 'run', 'build'
	doLast {
		println "Frontend build completed."
	}
}

tasks.register('copyFrontend', Copy) {
	from '../client/build'
	into 'src/main/resources/static'
	doLast {
		println "Frontend files copied to 'src/main/resources/static'."
	}
}

copyFrontend.mustRunAfter buildFrontend

tasks.register('buildAndCopyFrontend') {
	dependsOn 'buildFrontend', 'copyFrontend'
}

tasks.named('build') {
	dependsOn 'buildAndCopyFrontend'
}

